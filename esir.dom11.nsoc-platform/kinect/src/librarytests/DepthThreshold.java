package librarytests;


import org.openkinect.*;
import org.openkinect.processing.*;

import processing.core.PApplet;
import processing.core.PImage;

public class DepthThreshold extends PApplet {
	// Elie Zananiri
	// Depth thresholding example
	// http://www.silentlycrashing.net

	Kinect kinect;
	int kWidth  = 640;
	int kHeight = 480;
	int kAngle  =  15;

	PImage depthImg;
	int minDepth =  60;
	int maxDepth = 860;

	public void setup() {
	  size(kWidth*2, kHeight);

	  kinect = new Kinect(this);
	  kinect.start();
	  kinect.enableDepth(true);
	  kinect.tilt(kAngle);

	  depthImg = new PImage(kWidth, kHeight);
	}

	public void draw() {
	  // draw the raw image
	  image(kinect.getDepthImage(), 0, 0);

	  // threshold the depth image
	  int[] rawDepth = kinect.getRawDepth();
	  for (int i=0; i < kWidth*kHeight; i++) {
	    if (rawDepth[i] >= minDepth && rawDepth[i] <= maxDepth) {
	      depthImg.pixels[i] = 0xFFFFFFFF;
	    } else {
	      depthImg.pixels[i] = 0;
	    }
	  }

	  // draw the thresholded image
	  depthImg.updatePixels();
	  image(depthImg, kWidth, 0);

	  fill(0);
	  text("TILT: " + kAngle, 10, 20);
	  text("THRESHOLD: [" + minDepth + ", " + maxDepth + "]", 10, 36);
	}

	public void keyPressed() {
	  if (key == CODED) {
	    if (keyCode == UP) {
	      kAngle++;
	    } else if (keyCode == DOWN) {
	      kAngle--;
	    }
	    kAngle = constrain(kAngle, 0, 30);
	    kinect.tilt(kAngle);
	  }
	  
	  else if (key == 'a') {
	    minDepth = constrain(minDepth+10, 0, maxDepth);
	  } else if (key == 's') {
	    minDepth = constrain(minDepth-10, 0, maxDepth);
	  }
	  
	  else if (key == 'z') {
	    maxDepth = constrain(maxDepth+10, minDepth, 2047);
	  } else if (key =='x') {
	    maxDepth = constrain(maxDepth-10, minDepth, 2047);
	  }
	}

	public void stop() {
	  kinect.quit();
	  super.stop();
	}
}
